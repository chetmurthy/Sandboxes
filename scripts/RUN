#!/usr/bin/perl -w

use strict ;
use Carp;
use Carp::Assert;
use YAML::Tiny;
use Data::Dumper ;

our $usage ;
our $verbose ;
our $chdir ;
our $rc ;
our @sandboxes = () ;

while(@ARGV) {
  if ($ARGV[0] eq '-rcfile') {
    shift @ARGV ;
    my $rcfile = shift @ARGV ;
    $rc = YAML::Tiny->read($rcfile)
    || confess "cannot read rcfile $rcfile" ;
    $rc = $rc->[0] || confess "malformed YAML: ".Dumper($rc) ;
  }
  elsif ($ARGV[0] eq '-v') {
    shift @ARGV ; $verbose = 1 ;
  }
  elsif ($ARGV[0] eq '-h') {
    shift @ARGV ; $usage = 1 ;
  }
  elsif ($ARGV[0] eq '-C') {
    shift @ARGV; $chdir = shift @ARGV;
  }
  elsif ($ARGV[0] eq '-s') {
    shift @ARGV;
    my $t = shift @ARGV;
    push(@sandboxes, split(/\s*,\s*/, $t)) ;
  }
  else { last ; }
}

unless ($rc) {
  $rc = YAML::Tiny->read("$ENV{HOME}/.sandbox/rc.yaml")
    || confess "cannot read rcfile $ENV{HOME}/.sandbox/rc.yaml";
  $rc = $rc->[0] || confess "malformed YAML: ".Dumper($rc) ;
}

if ($usage) {
  usage(); exit ;
}

our $rcfile ;
{
  my $txt = "";
  $txt .= "unset BASH_ENV\n" ;
  $txt .= <<"EOF";
if [ -f ~/.bashrc ]; then . ~/.bashrc; fi
EOF
  foreach my $s (@sandboxes) {
    $txt .= dosandbox($s) ;
    sbpush($s) ;
  }

  if ($chdir) {
    $txt .= "cd $chdir\n";
  }

  $rcfile = "/tmp/RUN.bash$$";
  f_write($rcfile, $txt) ;
  $ENV{BASH_ENV} = $rcfile ;
}
END {
  unlink($rcfile) if $rcfile ;
}
our $prog = shift @ARGV;

our $args = @ARGV ? (" '".join("' '",@ARGV)."'") : "";
our $cmd = "$prog$args";
$cmd = "bash --rcfile $rcfile -c \"$cmd\"" if ($prog ne 'bash') ;
print STDERR $cmd."\n" if $main::verbose ;
v_system($cmd);

sub usage {
  print "Available base sandboxes:\n";
  foreach my $s (sort (keys %{ $rc->{sandboxes} })) {
    my $desc = "No description available" ;
    $desc = $rc->{sandboxes}->{$s}->{description} if exists $rc->{sandboxes}->{$s}->{description} ;
    print "\t$s - $desc\n" ;
  }
  print "Composite sandboxes:\n";
  foreach my $s (sort (keys %{ $rc->{composite} })) {
    my @layers = @{ $rc->{composite}->{$s} } ;
    print "$s - ".join(" THEN ",@layers)."\n";
  }
}

sub dosandbox {
  my $s = shift;

  my $txt = "";

  if (exists $rc->{'composite'}->{$s}) {
    my @sl = @{ $rc->{'composite'}->{$s} } ;
    foreach my $subs (@sl) {
      $txt .= dosandbox($subs) ;
    }
  }
  elsif (exists $rc->{'sandboxes'}->{$s}) {
    my $sbh = $rc->{'sandboxes'}->{$s} ;
    my $dir = envsubst($sbh->{'directory'}) ;
    my $rc = $sbh->{'rc'} ;
    my $varname = $sbh->{'varname'} ;

    $txt .= "export $varname=$dir\n";
    
    if (exists $sbh->{'set'}) {
      my %set = %{$sbh->{'set'}} ;
      foreach my $k (keys %set) {
	$txt .= "export $k=\"".quotemeta($set{$k})."\"\n";
      }
    }

    my @rcfiles ;
    if (ref($rc) eq 'ARRAY') {
      @rcfiles = @{ $rc } ;
    }
    else {
      push(@rcfiles, $rc) ;
    }
    foreach my $f (@rcfiles) {
      my $absf = selectrc(envsubst($f)) ;
      $txt .= dorc($dir, $absf) ;
    }

    if (exists $sbh->{'chdir'}) {
      $main::chdir = $sbh->{'chdir'} ;
    }
  }
  else { confess "unknown sandbox $s" ; }

  return $txt ;
}

sub dorc {
  my $sbdir = shift ;
  my $rcfile = shift ;

  my $wd = `pwd` ;
  chomp $wd ;

  my $txt = <<"EOF";
cd $sbdir
. $rcfile
cd $wd
EOF

  return $txt ;
}

sub envsubst {
  my $txt = shift ;

  my $result = "" ;
  while ($txt =~ s,(\$[a-z][a-z0-9]*|\$\{[a-z][a-z0-9]*\}),,si) {
    $result .= $` . env($1) ;
    $txt = $' ;
  }
  $result .= $txt ;

  return $result ;
}

sub env {
  my $x = shift ;
  my $varna ;
  if ($x =~ m,\$([a-z][a-z0-9]*),i) {
    $varna = $1 ;
  }
  elsif ($x =~ m,\$\{([a-z][a-z0-9]*)\},i) {
    $varna = $1 ;
  }
  else { confess "agh!  bad env var <<$x>>" ; }

  if (exists $ENV{$varna}) {
    return $ENV{$varna} ;
  }
  else { return "" ; }
}

sub bashdot {
  my $txt = join("\n",@_)."\n" ;

  open(DOT,">/tmp/tmp$$") || confess "basndot: cannot open open /tmp/tmp$$ for write" ;
  print DOT $txt ;
  print DOT "echo \"--RoNnIeRaYgUn--\"\n" ;
  print DOT "printenv\n";
  close(DOT) ;

  open(BASH,"bash /tmp/tmp$$ |") || confess "cannot run bash script /tmp/tmp$$" ;
  while(<BASH>) {
    last if /--RoNnIeRaYgUn--/ ;
    print ;
  }
  my %h = () ;
  while(<BASH>) {
    next unless /\S/ ;
    confess "bad printenv line <<$_>>"
      unless (/^([^=\s]+)=(.*)$/) ;

    my $lhs = $1 ;
    my $rhs = $2 ;

    next if $lhs eq '_' ;
    next if $lhs eq 'SHLVL' ;

    if (!(exists $ENV{$lhs}) || $rhs ne $ENV{$lhs}) {
      $h{$lhs} = $rhs ;
    }
  }
  print Dumper(\%h) if $main::verbose ;

  foreach my $k (sort (keys %h)) {
    print STDERR "export $k=$h{$k}\n" if $main::verbose ;
    $ENV{$k} = $h{$k} ;
  }
}

sub v_system {
  my $cmd = shift;
  print STDERR "<<$cmd>>\n" if ($main::verbose);
  system $cmd;
}

sub selectrc {
  my $f = shift ;

  my $h = `hostname` ;
  chomp $h ;

  return "$f.$h" if (-r "$f.$h") ;
  return $f if (-r $f) ;
  confess "no rcfile $f" ;
}

sub sbpush {
  my $tok = shift ;

  if (exists $ENV{SANDBOX}) {
    $ENV{SANDBOX} =~ s/\[(.+)\]/ "[$1:$tok]" /e ;
  }
  else {
    $ENV{SANDBOX} = "[$tok]" ;
  }
}

sub f_write {
  my $f = shift;
  open(F,">$f") || die "cannot open $f for writing";
  print F @_;
  close(F);
}
